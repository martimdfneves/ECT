<!DOCTYPE html>

<html>

<head>
	
<!--

WebGL_example_25.html

Phong Illumination Model on the Vertex-Shader - Reading normal vectors from file

J. Madeira - October 2015 + November 2018

-->
	
<title>WebGL example 25</title>

<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<!--

The vertex and fragment shaders.

NEW VERTEX SHADER!

The fragment shader is the same simple one.

-->

<script id="shader-vs" type="x-shader/x-vertex">
	
    attribute vec3 vPosition;

    uniform mat4 uMVMatrix;

    uniform mat4 uPMatrix;
    
    varying vec4 fColor;
    
    // For the Phong Illumination Model - NEW
    
    attribute vec3 vNormal;
    
    uniform vec4 viewerPosition;
	
    // NEW - Array of lights
    
	#define MAX_LIGHTS 5

	uniform int numLights;

	uniform struct Light {
	
		vec4 position;
   
		mat4 lightSourceMatrix; // for animation

		vec3 intensities; //a.k.a the color of the light
   
		vec3 ambientIntensities;
			
	} allLights[MAX_LIGHTS];
    
    // NEW - The material properties

	uniform vec3 k_ambient;
	
	uniform vec3 k_diffuse;
	
	uniform vec3 k_specular;
	
    uniform float shininess;
        
    void main(void) {
		
		// To allow seeing the points drawn
		
		gl_PointSize = 5.0;
		
		// Just converting the (x,y,z) vertices to Homogeneous Coord.
		
		// And multiplying by the Projection and the Model-View matrix
		
        gl_Position = uPMatrix * uMVMatrix * vec4(vPosition, 1.0);
        
        // Phong Illumination Model
        
        // pos is vertex position after applying the global transformation
    
        vec3 pos = (uMVMatrix * vec4(vPosition, 1.0)).xyz;
    
		// NEW - ITERATING THROUGH ALL LIGHT SOURCES

		fColor = vec4(0.0, 0.0, 0.0, 0.0);	// To add all illumination components
		 
		// THE FOR LOOP NEEDS A STOPPING CONSTANT VALUE
		
		for( int i = 0; i < MAX_LIGHTS; i++ ) 
		{
			if( i == numLights )
			
				break;
				
			// Ambient component is constant for each light source
        
			vec4 ambient = vec4( k_ambient * allLights[i].ambientIntensities, 1.0 );
	   
			fColor += ambient;
		
           // vector from vertex position to light source
    
           vec3 L;
        
           // check for directional light
    
           if(allLights[i].position.w == 0.0) 
        
				L = normalize( (allLights[i].lightSourceMatrix * allLights[i].position).xyz );
        
           else 
        
			    L = normalize( (allLights[i].lightSourceMatrix * allLights[i].position).xyz - pos );
			
		   // Vector from the vertex position to the eye
		
		   vec3 E;
		
		   // The viewer is at the origin or at an indefinite distance
		   // on the ZZ axis
		
		   if(viewerPosition.w == 1.0) 
        
			   // At the origin
			
			   E = -normalize( pos );
        
           else 
        
			   // On the ZZ axis
			
			   E = vec3(0,0,1);
		    
           // Halfway vector
    
           vec3 H = normalize( L + E );

           // Transform vertex normal into eye coordinates
    
           vec4 N = normalize( uMVMatrix * vec4(vNormal, 0.0));

           // Compute terms in the illumination equation
        
		   // Diffuse component 
		
		   float dotProductLN = L[0] * N[0] + L[1] * N[1] + L[2] * N[2]; 
		
           float cosNL = max( dotProductLN, 0.0 );
    
           vec4  diffuse = vec4( k_diffuse * cosNL * allLights[i].intensities, 1.0 );
        
           // Specular component

           float dotProductNH = N[0] * H[0] + N[1] * H[1] + N[2] * H[2]; 
		
		   float cosNH = pow( max( dotProductNH, 0.0 ), shininess );
    
           vec4  specular = vec4( k_specular * cosNH * allLights[i].intensities, 1.0 );
    
           if( dotProductLN < 0.0 ) {
	
		      specular = vec4(0.0, 0.0, 0.0, 1.0);
           } 

		   // Adding the components
		
           fColor += diffuse + specular;
	   }
	}
    
</script>

<script id="shader-fs" type="x-shader/x-fragment">

	precision mediump float;
	
	varying vec4 fColor;
	
	void main(void) {
		
		// Using the passed vertex color
		
		gl_FragColor = fColor;
	}

</script>

<!-- The JS files -->

<!-- Some useful functions for browser compatibility -->

<script type="text/javascript" src="webgl-utils.js"></script>

<!-- Handling vectors and matrices -->

<script type="text/javascript" src="maths.js"></script>

<!-- Processing triangle mesh models -->

<script type="text/javascript" src="models.js"></script>

<!-- Creating the light sources -->

<script type="text/javascript" src="lightSources.js"></script>

<!-- WebGL code -->

<script type="text/javascript" src="initShaders.js"></script>

<script type="text/javascript" src="WebGL_example_25_GPU_per_vertex.js"></script>

</head>

<body onload="runWebGL();">

	<div style="text-align: center">
		
		<p>Displaying a model read from a .txt or a .obj file - Per vertex shading on the GPU</p>
		
		<canvas id="my-canvas" style="border:1px solid #000000;" width="350" height="350"></canvas>

		<p>Model XX rotation: 
			
		<button id="XX-on-off-button">On / Off</button>

	    <button id="XX-direction-button">Change Direction</button>

	    <button id="XX-slower-button">Slower</button>

	    <button id="XX-faster-button">Faster</button>

		</p>
		
		<p>Model YY rotation: 
			
		<button id="YY-on-off-button">On / Off</button>

	    <button id="YY-direction-button">Change Direction</button>

	    <button id="YY-slower-button">Slower</button>

	    <button id="YY-faster-button">Faster</button>

		</p>
		
		<p>Model ZZ rotation: 
			
		<button id="ZZ-on-off-button">On / Off</button>

	    <button id="ZZ-direction-button">Change Direction</button>

	    <button id="ZZ-slower-button">Slower</button>

	    <button id="ZZ-faster-button">Faster</button>

		</p>
		
		<p>Choose the <b>projection type</b>:
		
        <select id="projection-selection">
			<option value="0">Orthogonal Projection</option>
			<option value="1">Perspective Projection</option>
        </select>
        
        </p>
        
        <p>Choose the <b>rendering mode</b>:
		
        <select id="rendering-mode-selection">
			<option value="0">Filled Triangles</option>
			<option value="1">Wireframe</option>
			<option value="2">Vertices</option>
        </select>
        
        <p>Read a 3D model from a <b>OBJ file</b>:
		
		<input type="file" id="obj-file" name="file">
		
		</p>
		
        <p>Read a 3D model from a <b>text file</b>:
		
		<input type="file" id="text-file" name="file">
		
		</p>
		
        <p>
		
		<button id="reset-button">Reset</button>
		
		</p>
				
	</div>

</body>

</html>
